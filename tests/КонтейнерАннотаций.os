#Использовать ".."
#Использовать "."
#Использовать asserts

&Тест
Процедура ПустойКонтейнерСодержитБазовуюАннотацию() Экспорт

	// Дано
	КонтейнерАннотаций = Новый КонтейнерАннотаций();
	
	// Когда
	ОпределенияАннотаций = КонтейнерАннотаций.ПолучитьОпределенияАннотаций();

	// Тогда
	Ожидаем.Что(ОпределенияАннотаций).ИмеетДлину(1);

	ОпределениеАннотации = ОпределенияАннотаций.Получить("аннотация");

	Ожидаем.Что(ОпределениеАннотации).ИмеетТип("ОпределениеАннотации");
	Ожидаем.Что(ОпределениеАннотации.Имя()).Равно("Аннотация");
	Ожидаем.Что(ОпределениеАннотации.ИмяТипаАннотации()).Равно("АннотацияАннотация");
	Ожидаем.Что(ОпределениеАннотации.Аннотации()).ИмеетДлину(1);
	Ожидаем.Что(ОпределениеАннотации.Параметры()).ИмеетДлину(1);
	
КонецПроцедуры

&Тест
Процедура МожноДобавитьСвоюАнннотацию() Экспорт
	
	// Дано
	КонтейнерАннотаций = Новый КонтейнерАннотаций();
	
	// Когда
	КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияТестоваяАннотация"));

	// Тогда
	ОпределениеАннотации = КонтейнерАннотаций.ПолучитьОпределениеАннотации("ТестоваяАннотация");

	Ожидаем.Что(ОпределениеАннотации).ИмеетТип("ОпределениеАннотации");
	Ожидаем.Что(ОпределениеАннотации.Имя()).Равно("ТестоваяАннотация");
	Ожидаем.Что(ОпределениеАннотации.ИмяТипаАннотации()).Равно("АннотацияТестоваяАннотация");
	Ожидаем.Что(ОпределениеАннотации.Аннотации()).ИмеетДлину(1);
	Ожидаем.Что(ОпределениеАннотации.Параметры()).ИмеетДлину(1);

КонецПроцедуры

&Тест
Процедура НельзяДобавитьОднуИТуЖеАннотациюДважды() Экспорт
	
	// Дано
	КонтейнерАннотаций = Новый КонтейнерАннотаций();
	КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияТестоваяАннотация"));
	
	// Когда, Тогда
	Ожидаем.Что(
		"КонтейнерАннотаций.ДобавитьАннотацию(Тип(""АннотацияТестоваяАннотация""))"
	).ВыбрасываетИсключение(
		"Аннотация с именем ""ТестоваяАннотация"" уже зарегистрирована в контейнере"
	);

КонецПроцедуры

&Тест
Процедура МожноПереопределитьАннотациюСФлагомПереопределения() Экспорт
	
	// Дано
	КонтейнерАннотаций = Новый КонтейнерАннотаций();
	КонтейнерАннотаций.ДобавитьАннотацию(Тип("АннотацияТестоваяАннотация"));
	
	// Когда
	ОпределениеАннотации = КонтейнерАннотаций.ДобавитьАннотацию(
		Тип("АннотацияТестоваяАннотация"), 
		Истина // ПереопределитьЕслиСуществует
	);

	// Тогда
	Ожидаем.Что(ОпределениеАннотации).ИмеетТип("ОпределениеАннотации");
	Ожидаем.Что(ОпределениеАннотации.Имя()).Равно("ТестоваяАннотация");
	Ожидаем.Что(ОпределениеАннотации.ИмяТипаАннотации()).Равно("АннотацияТестоваяАннотация");

КонецПроцедуры
